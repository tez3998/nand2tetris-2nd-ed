// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // xの前処理
    // zx
    Mux16(a=x, b=false, sel=zx, out=intX1);
    // nx
    Not16(in=intX1, out=notX);
    Mux16(a=intX1, b=notX, sel=nx, out=intX2);

    // yの前処理
    // zy
    Mux16(a=y, b=false, sel=zy, out=intY1);
    // ny
    Not16(in=intY1, out=notY);
    Mux16(a=intY1, b=notY, sel=ny, out=intY2);

    // 演算
    // x+y
    Add16(a=intX2, b=intY2, out=arithResult);
    // x&y
    And16(a=intX2, b=intY2, out=logicResult);
    // 演算の選択
    Mux16(a=logicResult, b=arithResult, sel=f, out=result);

    // 演算結果の後処理
    Not16(in=result, out=notResult);
    Mux16(a=result, b=notResult, sel=no, out=out);

    // zr
    Mux16(a=result, b=notResult, sel=no, out[0..7]=out1stHalf, out[8..15]=out2ndHalf);
    Or8Way(in=out1stHalf, out=zr1stHalf);
    Or8Way(in=out2ndHalf, out=zr2ndHalf);
    Or(a=zr1stHalf, b=zr2ndHalf, out=notZr);
    Not(in=notZr, out=zr);

    // ng
    Mux16(a=result, b=notResult, sel=no, out[15]=ng);
}